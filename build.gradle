
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.5.6.RELEASE'
    }
}
apply plugin: 'scala' 
apply plugin: 'java'
apply plugin: 'org.springframework.boot'
 
jar {
    baseName = 'webservice'
    version = '0.1.0'
}
 
repositories {
    mavenCentral()
}
 
sourceCompatibility = 1.8
targetCompatibility = 1.8
 
dependencies {
    compile('org.springframework.boot:spring-boot-starter-web')
    testCompile('io.gatling.highcharts:gatling-charts-highcharts:2.3.0')
    compile('org.springframework.boot:spring-boot-starter-actuator')
}

/**
 * Gatling load tests
 */
task testLoad(type: JavaExec) {
    description = 'Test load the Spring Boot web service with Gatling'
    group = 'Load Test'
    classpath = sourceSets.test.runtimeClasspath
    jvmArgs = [
            // workaround for https://github.com/gatling/gatling/issues/2689
            "-Dgatling.core.directory.binaries=${sourceSets.test.output.classesDir.toString()}",
            "-Dlogback.configurationFile=${logbackGatlingConfig()}"
    ]
    main = 'io.gatling.app.Gatling'
    args = [
            '--simulation', 'webservice.gatling.simulation.WebServiceCallSimulation',
            '--results-folder', "${buildDir}/gatling-results",
            '--binaries-folder', sourceSets.test.output.classesDir.toString() // ignored because of above bug       
    ]
}

def logbackGatlingConfig() {
    return sourceSets.test.resources.find { it.name == 'logback-gatling.xml'};
}